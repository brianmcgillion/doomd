;;;; $DOOMDIR/config.el -*- lexical-binding: t; -*-

* Interface Tweaks
:PROPERTIES:
:ID:       7d3f0a7b-101c-44cd-920c-65a82bc21877
:END:
#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_Interface_Tweaks
;;;
;;;

;; When starting a new frame make it maximized
(add-to-list 'initial-frame-alist '(fullscreen . fullboth))
(push '(fullscreen . fullboth)   default-frame-alist)

;; Set your own banner to replace the default doom one "convert image.png
;; -resize 600 -quality 75 OUTPUT.png" try resize 400 for smaller resolutions
;; e.g. 1080p
(setq! fancy-splash-image (concat doom-private-dir "banner/trancendent-gnu.png"))

;; Doom exposes five (optional) variables for controlling fonts in Doom. Here
;; are the three important ones:
;;
;; + `doom-font'
;; + `doom-variable-pitch-font'
;; + `doom-big-font' -- used for `doom-big-font-mode'; use this for
;;   presentations or streaming.
;;
;; They all accept either a font-spec, font string ("Input Mono-12"), or xlfd
;; font string. You generally only need these two:
;;(setq doom-font (font-spec :family "monospace" :size 14))

;; There are two ways to load a theme. Both assume the theme is installed and
;; available. You can either set `doom-theme' or manually load a theme with the
;; `load-theme' function. This is the default:
(setq! doom-theme 'doom-dracula)

(setq! doom-font (font-spec :family "FiraCode Nerd Font Mono" :size 13)
       doom-variable-pitch-font (font-spec :family "Fira Sans" :size 13)
       doom-big-font (font-spec :family "FiraCode Nerd Font Mono" :size 19)
       doom-serif-font (font-spec :family "Source Serif 4" :size 13)
       doom-symbol-font (font-spec :family "Font Awesome 7 Free"))

(after! doom-themes
  (setq! doom-themes-enable-bold t
         doom-themes-enable-italic t))

;; Fix gnus face inheritance cycle that breaks corfu child frames
;; The doom-dracula theme creates a circular inheritance in gnus faces
(after! gnus
  (custom-set-faces!
    '(gnus-group-news-low :inherit nil)
    '(gnus-group-news-low-empty :inherit gnus-group-news-low :weight normal)))

(after! org-modern
  (set-face-attribute 'org-modern-symbol nil :family "Font Awesome 7 Free")
  (set-face-attribute 'org-modern-label nil :height 1.0)
  (set-face-attribute 'org-modern-block-name nil :height 1.0))

;; be as colorful as possible.
(setq treesit-font-lock-level 4)

;; This determines the style of line numbers in effect. If set to `nil', line
;; numbers are disabled. For relative line numbers, set this to `relative'.
(setq! display-line-numbers-type t)

;; Blinking cursors are annoying
(blink-cursor-mode -1)

(setq! +doom-dashboard-menu-sections
       '(("Open org-agenda"
          :icon (nerd-icons-octicon "nf-oct-calendar" :face 'doom-dashboard-menu-title)
          :action bmg/switch-to-agenda)
         ("Recently opened files"
          :icon (nerd-icons-faicon "nf-fa-file_text" :face 'doom-dashboard-menu-title)
          :action recentf-open-files)
         ("Reload last session"
          :icon (nerd-icons-octicon "nf-oct-history" :face 'doom-dashboard-menu-title)
          :when (cond ((modulep! :ui workspaces)
                       (file-exists-p (expand-file-name persp-auto-save-fname persp-save-dir)))
                      ((require 'desktop nil t)
                       (file-exists-p (desktop-full-file-name))))
          :action doom/quickload-session)
         ("Open project"
          :icon (nerd-icons-octicon "nf-oct-briefcase" :face 'doom-dashboard-menu-title)
          :action projectile-switch-project)
         ("Open private configuration"
          :icon (nerd-icons-octicon "nf-oct-tools" :face 'doom-dashboard-menu-title)
          :when (file-directory-p doom-private-dir)
          :action doom/open-private-config)
         ("Open documentation"
          :icon (nerd-icons-octicon "nf-oct-book" :face 'doom-dashboard-menu-title)
          :action doom/help)
         ;; ("Passwords"
         ;;  :icon (icons-octicon "lock" :face 'font-lock-keyword-face)
         ;;  :action pass)
         ("RSS"
          :icon (nerd-icons-octicon "nf-oct-rss" :face 'doom-dashboard-menu-title)
          :action =rss)
         ))
;;;
;;;
;;; END_Interface_Tweaks
;;;
;;;

#+END_SRC

* General
:PROPERTIES:
:ID:       5fa6e40a-6235-4da7-9c35-3df39775a7af
:END:

#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_General
;;;
;;;

;; Some functionality uses this to identify you, e.g. GPG configuration, email
;; clients, file templates and snippets.
(setq! user-full-name "Brian McGillion"
       user-mail-address "brian@ssrc.tii.ae"
       ;; Who remembers all the commands :)
       which-key-idle-delay 0.5
       ;; Prefer encrypted auth source
       auth-sources '("~/.authinfo.gpg" "~/.netrc")
       ;; M-x projectile-discover-projects-in-directory
       ;; M-x projectile-discover-projects-in-search-path
       projectile-project-search-path '(("~/.dotfiles" . 1)("~/projects" . 6)("~/.config" . 2)("~/Documents/org" . 2))
       projectile-auto-discover nil)

;; TODO WTF is happening here ?? enable word-wrap (almost) everywhere
;;(+global-word-wrap-mode +1)

;; TODO: Enable word-wrap for text modes manually if global wrap fails
;; (add-hook 'org-mode-hook #'visual-line-mode)
;; (add-hook 'markdown-mode-hook #'visual-line-mode)
;; (add-hook 'text-mode-hook #'visual-line-mode)

;; If a file changes on disk update the buffer to match
(global-auto-revert-mode t)

(dirvish-side-follow-mode t)

;; Use a custom dictionary
(setq! ispell-dictionary (if IS-MAC "en_GB" "en_US"))

;; NixOS doesn't have /usr/share/dict/words, disable word-list completion in text-mode
;; (aspell handles spell-checking, this only affects M-TAB word completion)
(setq! text-mode-ispell-word-completion nil)

(set-language-environment-charset "UTF-8")

;; Use the Languagetool only in server mode
(setq! langtool-http-server-host "localhost"
       langtool-http-server-port 8081
       langtool-default-language nil)

(after! license-snippets
  (license-snippets-init))

;; load all the elfeed configurations
(load! "elfeed-config.el")

;; misc selection of useful functions
(use-package! crux
  :defer t)

;; Clone git repos or jump to the project in the code path
;; https://github.com/NinjaTrappeur/my-repo-pins
(use-package! my-repo-pins
  :after-call my-repo-pins
  :defer t
  :init
  (setq my-repo-pins-code-root "~/projects/code"))

(use-package! inheritenv
  :config
  ;; ensure that the environment is carried over to the subshell that is called.
  ;; this was tested against go-mode specifically go-import-add
  (inheritenv-add-advice #'process-lines)
  (inheritenv-add-advice #'shell-command-to-string))

(after! consult
  ;; Needed to ensure that preview is working live, without the need to
  ;; C-SPC everytime you need to see a preview. see vertico/config.el
  ;; where we are overriding the override :)
  (consult-customize
   +default/search-project +default/search-other-project
   +default/search-project-for-symbol-at-point
   +default/search-cwd +default/search-other-cwd
   +default/search-notes-for-symbol-at-point
   +default/search-emacsd
   consult-ripgrep consult-git-grep consult-grep
   consult-bookmark consult-recent-file
   consult--source-recent-file consult--source-project-recent-file consult--source-bookmark
   :preview-key (list "C-SPC" :debounce 0.1 'any)))


;; Try to stop killing emacs with C-x C-c and use C-c q f
;; when using the daemon mode.
(defun my-confirm-kill-daemon (prompt)
  "Ask whether to kill daemon Emacs with PROMPT.
Intended as a predicate for `confirm-kill-emacs'."
  (or (not (daemonp))
      (yes-or-no-p prompt)))

(setq! confirm-kill-emacs #'my-confirm-kill-daemon)

;;;###autoload
(defun bmg/api-key-from-auth-source (&optional host user)
  "Lookup api key in the auth source.
By default, the LLM host for the active backend is used as HOST,
and \"apikey\" as USER."
  (if-let ((secret
            (plist-get
             (car (auth-source-search
                   :host (or host)
                   :user (or user "apikey")
                   :require '(:secret)))
             :secret)))
      (if (functionp secret)
          (encode-coding-string (funcall secret) 'utf-8)
        secret)
    (user-error "No API key found for host: %s, user: %s. Check your auth-source file."
                (or host "default") (or user "apikey"))))

;;;
;;;
;;; END_General
;;;
;;;
#+END_SRC

* Code
:PROPERTIES:
:ID:       4460f37d-9944-4717-acf5-e2ab1e410787
:END:
#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_code
;;;
;;;

;; Add handlers SELinux files
(add-to-list 'auto-mode-alist '("\\.te\\'" . m4-mode))
(add-to-list 'auto-mode-alist '("\\.mdx\\'" . markdown-mode))
(add-to-list 'auto-mode-alist '("\\.mdx\\'" . gfm-mode)) ;; GitHub Flavored Markdown

;; make a shell script executable automatically on save
(add-hook 'after-save-hook
          'executable-make-buffer-file-executable-if-script-p)

(setq c-default-style "linux") ;; set style to "linux"

;;setup clangd lsp
(after! lsp-clangd
  (setq lsp-clients-clangd-args
        '("-j=3"
          "--background-index"
          "--clang-tidy"
          "--completion-style=detailed"
          "--header-insertion=never"
          "--header-insertion-decorators=0"))
  (set-lsp-priority! 'clangd 2))

;; use just mode for setting tasks to run at the cmdline
(use-package! justl
  :config
  (map! :map justl-mode-map "e" 'justl-exec-recipe))

(after! lsp-mode
  (setq lsp-nix-nixd-server-path "nixd"
        lsp-nix-nixd-formatting-command ["nixfmt"]
        lsp-nix-nixd-nixpkgs-expr "import (builtins.getFlake \"/home/brian/.dotfiles\").inputs.nixpkgs { }"
        lsp-nix-nixd-nixos-options-expr "(builtins.getFlake \"/home/brian/.dotfiles\").nixosConfigurations.arcadia.options"))
;; Note: home-manager options commented out - home-manager appears to be integrated as a NixOS module
;; If you have standalone home-manager configs, use:
;; lsp-nix-nixd-home-manager-options-expr "(builtins.getFlake \"/home/brian/.dotfiles\").homeConfigurations.\"user@host\".options")


;;;
;;;
;;; END_code
;;;
;;;
#+END_SRC

* LLM/ML
#+begin_src emacs-lisp
;;;
;;;
;;; BEGIN_llm
;;;
;;;

;; Inline code completions via GitHub Copilot
;; Primary LLM interaction is via GitHub Copilot CLI (external)
;; Doom's :tools llm module provides gptel-magit for commit messages
(use-package! copilot
  :hook (prog-mode . copilot-mode)
  :bind (:map copilot-completion-map
              ("<tab>" . 'copilot-accept-completion)
              ("TAB" . 'copilot-accept-completion)
              ("C-TAB" . 'copilot-accept-completion-by-word)
              ("C-<tab>" . 'copilot-accept-completion-by-word))
  :config
  (setq! copilot-indent-offset-warning-disable t))

;;;
;;;
;;; END_llm
;;;
;;;
#+end_src

* Org
:PROPERTIES:
:ID:       b889f253-3691-41e3-a2ca-7f1c76f10d7d
:END:
#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_ORG
;;;
;;;

(defun bmg/switch-to-agenda ()
  "Switch to org-agenda overview view.
Opens the custom 'o' agenda command configured in
`org-agenda-custom-commands'."
  (interactive)
  (org-agenda nil "o"))

(defun bmg/archive-all-done ()
  "Archive all DONE tasks in the current buffer to the archive file."
  (interactive)
  (let ((count 0))
    (org-map-entries
     (lambda ()
       (org-archive-subtree)
       (setq count (1+ count))
       (setq org-map-continue-from (org-element-property :begin (org-element-at-point))))
     "/DONE" 'file)
    (message "Archived %d DONE tasks" count)))

;; change `org-directory'. It must be set before org loads!
(setq! org-directory "~/Documents/org/"
       org-archive-location (concat org-directory "/archive.org_archive::datetree/")
       org-id-link-to-org-use-id t
       org-ellipsis " â–¾ "
       org-startup-folded t
       org-src-fontify-natively t
       org-hide-emphasis-markers t
       org-modern-star 'replace)


(setq! org-roam-directory (file-truename (concat org-directory "roam/"))
       org-roam-extract-new-file-path "${slug}.org"
       org-default-notes-file (expand-file-name (format "inbox-%s.org" (system-name)) org-roam-directory)
       org-noter-notes-search-path org-roam-directory)

;; Point Doom's org-capture templates to roam inbox for unified capture
(setq! +org-capture-todo-file org-default-notes-file
       +org-capture-notes-file org-default-notes-file)

;; org-protocol for browser integration (Chrome/Firefox capture)
;; Requires desktop file handler configured in NixOS/home-manager
(require 'org-protocol)

;; Add org-protocol capture templates for web capture
;; "w" - Web page capture (link + optional selection)
;; "W" - Web page capture with immediate finish (quick bookmark)
(after! org
  (setq! org-protocol-default-template-key "w")
  (add-to-list 'org-capture-templates
               '("w" "Web Capture" entry
                 (file+headline org-default-notes-file "Inbox")
                 "* %?[[%:link][%:description]] :REFILE:web:\n:PROPERTIES:\n:CREATED: %U\n:SOURCE: %:link\n:END:\n%i"
                 :prepend t))
  (add-to-list 'org-capture-templates
               '("W" "Web Capture (quick)" entry
                 (file+headline org-default-notes-file "Inbox")
                 "* [[%:link][%:description]] :REFILE:web:\n:PROPERTIES:\n:CREATED: %U\n:SOURCE: %:link\n:END:\n%i"
                 :prepend t
                 :immediate-finish t)))

;; Org-roam capture templates
;; Uses subdirectories for organization + filetags for filtering
;; :mkdir t ensures directories are created on first use (multi-machine support)
(setq! org-roam-capture-templates
       '(;; Default note - flat in roam directory
         ("d" "default" plain "%?"
          :if-new (file+head "${slug}.org"
                   "#+title: ${title}\n#+created: %u\n#+last_modified: %U\n#+filetags:\n\n")
          :unnarrowed t)

         ;; Fleeting/quick note - appends to inbox with REFILE tag for GTD processing
         ("f" "fleeting" entry
          (file+headline org-default-notes-file "Inbox")
          "* %? :REFILE:\n:PROPERTIES:\n:CREATED: %U\n:END:\n%i"
          :prepend t)

         ;; Literature/reference note
         ("r" "reference" plain "%?"
          :if-new (file+head "refs/${slug}.org"
                   "#+title: ${title}\n#+created: %u\n#+roam_refs: ${ref}\n#+filetags: :reference:\n\n* Summary\n\n* Notes\n%?")
          :unnarrowed t
          :mkdir t)

         ;; Meeting note
         ("m" "meeting" plain "%?"
          :if-new (file+head "meetings/%<%Y%m%d>-${slug}.org"
                   "#+title: ${title}\n#+date: %<%Y-%m-%d>\n#+created: %u\n#+filetags: :meeting:\n\n* Attendees\n\n* Agenda\n\n* Notes\n%?\n\n* Action Items\n")
          :unnarrowed t
          :mkdir t)))

;; Org-roam dailies configuration
(setq! org-roam-dailies-directory "daily/")
(setq! org-roam-dailies-capture-templates
       '(("d" "default" entry
          "* %?"
          :target (file+head "%<%Y-%m-%d>.org"
                   "#+title: %<%Y-%m-%d %A>\n#+created: %u\n#+filetags: :daily:\n\n")
          :mkdir t)
         ("t" "task" entry
          "* TODO %?"
          :target (file+head+olp "%<%Y-%m-%d>.org"
                   "#+title: %<%Y-%m-%d %A>\n#+created: %u\n#+filetags: :daily:\n\n"
                   ("Tasks"))
          :mkdir t)
         ("j" "journal" entry
          "* %<%H:%M> %?"
          :target (file+head+olp "%<%Y-%m-%d>.org"
                   "#+title: %<%Y-%m-%d %A>\n#+created: %u\n#+filetags: :daily:\n\n"
                   ("Journal"))
          :mkdir t)))

;; Per-project capture system
;; Stores project files under roam/projects/{project-name}/ instead of in code repos
;; This keeps org files out of shared repositories while maintaining project organization

(defun bmg/get-project-name ()
  "Get current project name or prompt for one.
Uses projectile if in a project, otherwise offers existing project directories."
  (or (and (fboundp 'projectile-project-p)
           (projectile-project-p)
           (projectile-project-name))
      (completing-read "Project: "
                       (when (file-directory-p (expand-file-name "projects" org-roam-directory))
                         (directory-files
                          (expand-file-name "projects" org-roam-directory)
                          nil "^[^.]")))))

(defun bmg/project-org-file (filename)
  "Return path to FILENAME in current project's org directory.
Creates directory and initializes file with proper headers if needed."
  (let* ((project (bmg/get-project-name))
         (dir (expand-file-name (concat "projects/" project) org-roam-directory))
         (file (expand-file-name filename dir)))
    (unless (file-directory-p dir)
      (make-directory dir t))
    ;; Initialize file with proper headers if it doesn't exist
    (unless (file-exists-p file)
      (with-temp-file file
        (pcase filename
          ("todo.org"
           (insert (format "#+title: %s Tasks\n#+created: %s\n#+filetags: :project:todo:\n\n* Inbox\n\n* Active\n\n* Completed\n"
                           project (format-time-string "[%Y-%m-%d %a]"))))
          ("notes.org"
           (insert (format "#+title: %s Notes\n#+created: %s\n#+filetags: :project:notes:\n\n* Inbox\n\n* Notes\n"
                           project (format-time-string "[%Y-%m-%d %a]"))))
          ("changelog.org"
           (insert (format "#+title: %s Changelog\n#+created: %s\n#+filetags: :project:changelog:\n\n* Unreleased\n\n"
                           project (format-time-string "[%Y-%m-%d %a]"))))
          (_
           (insert (format "#+title: %s - %s\n#+created: %s\n#+filetags: :project:\n\n"
                           project (file-name-sans-extension filename)
                           (format-time-string "[%Y-%m-%d %a]")))))))
    file))

(defun bmg/capture-project-todo ()
  "Capture a TODO for the current project.
Adds to Inbox heading with REFILE tag for GTD processing."
  (interactive)
  (let ((file (bmg/project-org-file "todo.org")))
    (find-file file)
    (goto-char (point-min))
    (re-search-forward "^\\* Inbox" nil t)
    (org-end-of-subtree)
    (insert "\n** TODO  :REFILE:")
    (backward-char 8)))

(defun bmg/capture-project-note ()
  "Capture a note for the current project.
Adds to Inbox heading with REFILE tag for GTD processing."
  (interactive)
  (let ((file (bmg/project-org-file "notes.org")))
    (find-file file)
    (goto-char (point-min))
    (re-search-forward "^\\* Inbox" nil t)
    (org-end-of-subtree)
    (insert (format "\n** %s  :REFILE:" (format-time-string "[%Y-%m-%d %a]")))
    (backward-char 9)))

(defun bmg/capture-project-roam-note ()
  "Capture a roam note for the current project.
Creates a new org-roam node in projects/{project-name}/ directory."
  (interactive)
  (let* ((project (bmg/get-project-name))
         (org-roam-capture-templates
          `(("p" "project note" plain "%?"
             :if-new (file+head
                      ,(format "projects/%s/${slug}.org" project)
                      ,(format "#+title: ${title}\n#+created: %%u\n#+filetags: :project:%s:\n\n" project))
             :unnarrowed t
             :mkdir t))))
    (org-roam-capture nil "p")))

(defun bmg/open-project-todo ()
  "Open the TODO file for the current project."
  (interactive)
  (find-file (bmg/project-org-file "todo.org")))

(defun bmg/open-project-notes ()
  "Open the notes file for the current project."
  (interactive)
  (find-file (bmg/project-org-file "notes.org")))

;; Override Doom's project capture functions to use roam/projects/ instead of project-root
;; This makes C-c n n â†’ p â†’ t/n/c use our roam directory
(defadvice! bmg/org-capture-project-todo-file-a ()
  "Override to use roam/projects/{project}/todo.org instead of project-root."
  :override #'+org-capture-project-todo-file
  (bmg/project-org-file "todo.org"))

(defadvice! bmg/org-capture-project-notes-file-a ()
  "Override to use roam/projects/{project}/notes.org instead of project-root."
  :override #'+org-capture-project-notes-file
  (bmg/project-org-file "notes.org"))

(defadvice! bmg/org-capture-project-changelog-file-a ()
  "Override to use roam/projects/{project}/changelog.org instead of project-root."
  :override #'+org-capture-project-changelog-file
  (bmg/project-org-file "changelog.org"))

;; org-mem https://github.com/meedstrom/org-mem is a complimentary package to org roam
;; can use it ot extract certain properties of the roam db. In addition it also provides fast search functionality,
;; such as the ability to populate the org aganda file list with only relevant TODO items
(use-package! org-mem
  :after org
  :config
  (setq! org-mem-watch-dirs (list org-roam-directory)
         ;;org-mem-do-sync-with-org-id t
         org-roam-db-update-on-save nil
         org-mem-roamy-do-overwrite-real-db t
         ;; Reduce GC during org-roam operations
         org-roam-db-gc-threshold most-positive-fixnum)

  (defun my-set-agenda-files (&rest _)
    (setq! org-agenda-files
           (cl-loop
            for file in (org-mem-all-files)
            unless (string-search "archive" file)
            when (seq-find (lambda (entry)
                             (or (org-mem-entry-active-timestamps entry)
                                 (org-mem-entry-todo-state entry)
                                 (org-mem-entry-scheduled entry)
                                 (org-mem-entry-deadline entry)))
                           (org-mem-entries-in file))
            collect file)))
  (add-hook 'org-mem-post-full-scan-functions #'my-set-agenda-files)
  (org-mem-roamy-db-mode))

(setq! org-log-done 'time
       org-log-into-drawer t
       org-log-state-notes-insert-after-drawers nil)

(use-package! websocket
  :after org-roam)

(use-package! org-roam-ui
  :after org-roam
  :commands (org-roam-ui-mode))

;; citations and reference managment
(setq! citar-bibliography (concat org-directory "emacs_lit.bib")
       citar-library-paths '("~/Documents/Papers/")
       citar-notes-paths org-roam-directory)

(after! citar
  ;; define the keymap to allow citar to hand over to biblio to do
  ;; a reference lookup
  (defvar bmg/my-citar-embark-become-map
    (let ((map (make-sparse-keymap)))
      (define-key map (kbd "f") 'citar-open-library-files)
      (define-key map (kbd "x") 'biblio-arxiv-lookup)
      (define-key map (kbd "c") 'biblio-crossref-lookup)
      (define-key map (kbd "i") 'biblio-ieee-lookup)
      (define-key map (kbd "h") 'biblio-hal-lookup)
      (define-key map (kbd "s") 'biblio-dissemin-lookup)
      (define-key map (kbd "b") 'biblio-dblp-lookup)
      (define-key map (kbd "o") 'biblio-doi-insert-bibtex)
      map)
    "Citar Embark become keymap for biblio lookup."))

;; tell embark about the citar keymap after embark loads
(after! embark
  (add-to-list 'embark-become-keymaps 'bmg/my-citar-embark-become-map))


(setq! org-tag-alist '((:startgroup . nil)
                       ("@Project" . ?p)
                       ("@Reading" . ?r)
                       ("@Someday" . ?s)
                       ("@Training" . ?t)
                       ("@Courses" . ?c)
                       ("uni" . ?u)
                       ("@Research" . ?R)
                       ("Emacs" . ?e)
                       ("@Issue" . ?i)
                       ("Important" . ?I)
                       (:endgroup . nil)
                       (:startgroup . nil)
                       ("PERSONAL" . ?P)
                       (:endgroup . nil)))

(use-package! org-super-agenda
  :after org-agenda
  :init
  (setq!
   ;; more structured view
   org-agenda-prefix-format
   '((agenda . " %i %-20:c %?-12t %12s")
     (todo . " %i %-20:c ")
     (tags . " %i %-20:c ")
     (search . " %i %-20:c "))
   org-agenda-todo-keyword-format "%-6s"
   org-agenda-tags-column -120)

  (setq! org-agenda-time-grid '((daily today require-timed)
                                (800 1200 1600 2000)
                                "......"
                                "----------------")
         org-agenda-skip-scheduled-if-done t
         org-agenda-skip-deadline-if-done t
         org-agenda-include-deadlines t
         org-agenda-include-diary nil
         org-agenda-block-separator nil
         org-agenda-compact-blocks t
         org-agenda-start-with-log-mode t
         org-agenda-span 1
         org-agenda-start-day nil) ;; i.e. today

  ;; Custom agenda view aligned with Doom's org-todo-keywords
  ;; GTD workflow: Inbox/REFILE items appear at top for processing
  (setq! org-agenda-custom-commands
         '(("o" "Overview"
            ((agenda "" ((org-agenda-span 'week)
                         (org-agenda-start-on-weekday 0) ;; Sunday
                         (org-super-agenda-groups
                          '((:name "Today"
                             :time-grid t
                             :date today
                             :todo "TODAY"
                             :scheduled today
                             :order 1)))))
             (alltodo "" ((org-agenda-overriding-header "")
                          (org-super-agenda-groups
                           '((:name "ðŸ“¥ Inbox - Process these first"
                              :tag "REFILE"
                              :order 0)
                             (:name "Next to do"
                              :todo "TODO"
                              :order 3)
                             (:name "Ongoing"
                              :todo "STRT"
                              :order 3)
                             (:name "Recurring"
                              :todo "LOOP"
                              :order 4)
                             (:name "Personal"
                              :tag "PERSONAL"
                              :order 12)
                             (:name "Important"
                              :tag "Important"
                              :priority "A"
                              :order 6)
                             (:name "Due Today"
                              :deadline today
                              :order 2)
                             (:name "Due Soon"
                              :deadline future
                              :order 8)
                             (:name "Overdue"
                              :deadline past
                              :face error
                              :order 7)
                             (:name "Issues"
                              :tag "@Issue"
                              :order 12)
                             (:name "Emacs"
                              :tag "Emacs"
                              :order 13)
                             (:name "Projects"
                              :todo "PROJ"
                              :tag "@Project"
                              :order 14)
                             (:name "Research"
                              :tag "@Research"
                              :order 15)
                              (:name "Training/Courses"
                              :tag ("@Training" "@Courses")
                              :order 16)
                             (:name "To read"
                              :tag "@Reading"
                              :order 30)
                             (:name "Waiting"
                              :todo "HOLD"
                              :todo "WAIT"
                              :order 20)
                             (:name "University"
                              :tag "uni"
                              :order 32)
                             (:name "Someday"
                              :priority<= "C"
                              :tag "@Someday"
                              :todo "IDEA"
                              :order 90)
                             (:discard (:tag ("Chore" "Routine" "Daily")))))))))))
  :config
  (org-super-agenda-mode))

;; Helper functions for org workflow

(defun bmg/org-roam-review-week ()
  "Open all daily notes from the past week for review."
  (interactive)
  (require 'org-roam-dailies)
  (let ((files '()))
    (dotimes (i 7)
      (let* ((date (time-subtract (current-time) (days-to-time i)))
             (file (org-roam-dailies--daily-note-path date)))
        (when (file-exists-p file)
          (push file files))))
    (if files
        (progn
          (dolist (file (reverse files))
            (find-file-other-window file))
          (message "Opened %d daily notes from the past week" (length files)))
      (message "No daily notes found for the past week"))))

;;;
;;;
;;; END_ORG
;;;
;;;
#+END_SRC

* Map
:PROPERTIES:
:ID:       629b4ae3-039b-4729-b3f6-1ae18ed50d13
:END:
#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_Map
;;;
;;;

;; SPC z for org-roam - shorter than Doom's M-SPC m m / C-c l m
(after! org-roam
  (map! :leader
        (:prefix-map ("z" . "org-roam")
         "c" #'org-roam-capture
         "D" #'org-roam-demote-entire-buffer
         "f" #'org-roam-node-find
         "F" #'org-roam-ref-find
         "g" #'org-roam-graph
         "i" #'org-roam-node-insert
         "I" #'org-id-get-create
         "t" #'org-roam-buffer-toggle
         "T" #'org-roam-buffer-display-dedicated
         "r" #'org-roam-refile
         "R" #'org-roam-link-replace-all
         (:prefix ("d" . "dailies")
          "b" #'org-roam-dailies-goto-previous-note
          "d" #'org-roam-dailies-goto-date
          "D" #'org-roam-dailies-capture-date
          "f" #'org-roam-dailies-goto-next-note
          "m" #'org-roam-dailies-goto-tomorrow
          "M" #'org-roam-dailies-capture-tomorrow
          "n" #'org-roam-dailies-capture-today
          "t" #'org-roam-dailies-goto-today
          "T" #'org-roam-dailies-capture-today
          "y" #'org-roam-dailies-goto-yesterday
          "Y" #'org-roam-dailies-capture-yesterday
          "-" #'org-roam-dailies-find-directory)
         (:prefix ("o" . "node properties")
          "a" #'org-roam-alias-add
          "A" #'org-roam-alias-remove
          "t" #'org-roam-tag-add
          "T" #'org-roam-tag-remove
          "r" #'org-roam-ref-add
          "R" #'org-roam-ref-remove)
         (:prefix ("p" . "project")
          :desc "Project todo"      "t" #'bmg/capture-project-todo
          :desc "Project note"      "n" #'bmg/capture-project-note
          :desc "Project roam note" "p" #'bmg/capture-project-roam-note
          :desc "Open project todo" "T" #'bmg/open-project-todo
          :desc "Open project notes" "N" #'bmg/open-project-notes))))

(map!
 (;;: org-agenda
  (:leader
        ;;; <leader> n --- notes
   (:prefix ("n" . "notes")
    :desc "Org agenda"  "a" #'bmg/switch-to-agenda))

  (:map org-agenda-mode-map
        "i"                       #'org-agenda-clock-in
        "R"                       #'org-agenda-refile))

 (;;: open submenu
  (:leader
        ;;; <leader> o --- open
   (:prefix ("o" . "open")
    :desc "Url"  "u" #'browse-url
    :desc "Web"  "w" #'browse-url)))

 (;;: crux and stuff
  (:leader
        ;;;  <leader> b --- prelude
   (:prefix-map ("b" . "prelude")
    :desc "crux-cleanup-buffer-or-region"          "c" #'crux-cleanup-buffer-or-region
    :desc "crux-duplicate-current-line-or-region"  "d" #'crux-duplicate-current-line-or-region
    :desc "crux-delete-file-and-buffer"            "D" #'crux-delete-file-and-buffer
    :desc "crux-kill-other-buffers"                "k" #'crux-kill-other-buffers
    :desc "crux-open-with"                         "o" #'crux-open-with
    :desc "crux-rename-buffer-and-file"            "r" #'crux-rename-buffer-and-file
    :desc "crux-transpose-windows"                 "s" #'crux-transpose-windows
    :desc "crux-view-url"                          "u" #'crux-view-url
    :desc "crux-indent-defun"                      "TAB" #'crux-indent-defun
    :desc "Elfeed RSS enter"                       "e" #'=rss
    :desc "Aidermacs transient"                    "a" #'aidermacs-transient-menu)))
 ) ;; END MAP

(map! :map dirvish-mode-map
      ;; left click for expand/collapse dir or open file
      "<mouse-1>" #'dirvish-subtree-toggle-or-open
      ;; middle click for opening file / entering dir in other window
      "<mouse-2>" #'dired-mouse-find-file-other-window
      ;; right click for opening file / entering dir
      "<mouse-3>" #'dired-mouse-find-file
      "?"   #'dirvish-dispatch
      "q"   #'dirvish-quit
      "b"   #'dirvish-quick-access
      "f"   #'dirvish-file-info-menu
      "p"   #'dirvish-yank
      "S"   #'dirvish-quicksort
      "F"   #'dirvish-layout-toggle
      "z"   #'dirvish-history-jump
      "TAB" #'dirvish-subtree-toggle
      "M-b" #'dirvish-history-go-backward
      "M-f" #'dirvish-history-go-forward
      "M-n" #'dirvish-narrow
      "M-m" #'dirvish-mark-menu
      "M-s" #'dirvish-setup-menu
      "M-e" #'dirvish-emerge-menu
      (:prefix ("y" . "yank")
               "l"   #'dirvish-copy-file-true-path
               "n"   #'dirvish-copy-file-name
               "p"   #'dirvish-copy-file-path
               "r"   #'dirvish-copy-remote-path
               "y"   #'dired-do-copy)
      (:prefix ("s" . "symlinks")
               "s"   #'dirvish-symlink
               "S"   #'dirvish-relative-symlink
               "h"   #'dirvish-hardlink))

;;;
;;;
;;; END_MAP
;;;
;;;
#+END_SRC

* Elfeed Enhancements
#+BEGIN_SRC emacs-lisp
;;;
;;;
;;; BEGIN_Elfeed_Enhancements
;;;
;;;

(defun bmg/elfeed-mark-all-as-read ()
  "Mark all entries in current elfeed filter as read."
  (interactive)
  (when (yes-or-no-p "Mark all visible entries as read? ")
    (let ((count 0))
      (save-excursion
        (goto-char (point-min))
        (while (not (eobp))
          (when-let ((entry (elfeed-search-selected :ignore-region)))
            (elfeed-untag entry 'unread)
            (setq count (1+ count)))
          (forward-line)))
      (elfeed-search-update--force)
      (message "Marked %d entries as read" count))))

(after! elfeed
  (map! :map elfeed-search-mode-map
        "R" #'bmg/elfeed-mark-all-as-read))

;;;
;;;
;;; END_Elfeed_Enhancements
;;;
;;;
#+END_SRC
